@tool

# Editor script to generate the materials and cube scenes using the Kenney textures in the textures folder
extends Node

# Functions as a button to regenerate the materials and scenes
@export var generate := false
@export var debug    := true

@export var cube_scene_path          := "cube.tscn"
@export var cube_scene_absolute_path := false

var textures_path  := "textures"
var materials_path := "materials/"
var scenes_path    := "scenes/"

var cube_scene: Resource

func _ready():
	var base_dir   = get_script().resource_path.get_base_dir()

	textures_path  = base_dir.path_join(textures_path)
	materials_path = base_dir.path_join(materials_path) 
	scenes_path    = base_dir.path_join(scenes_path)

	var cube_scene_load_path
	if cube_scene_absolute_path:
		cube_scene_load_path = cube_scene_path
	else:
		cube_scene_load_path = base_dir.path_join(cube_scene_path)

	cube_scene = load(cube_scene_load_path)

	if debug:
		print("Textures Path     = %s" % textures_path)
		print("Materials Path    = %s" % materials_path)
		print("Scenes Path       = %s" % scenes_path)
		print("Cube Scene Path   = %s" % cube_scene_path)
		print("Cube Scene Loaded = %s" % (cube_scene != null))

# Generates the material and the scene corresponding to this color and texture
func generate_tex(col: String, tex_name: String):
	var id =  tex_name.trim_prefix("texture_").trim_suffix(".png")

	# Create the material and save it
	var mat := StandardMaterial3D.new()
	mat.albedo_texture = load(textures_path.path_join(col).path_join(tex_name))
	mat.vertex_color_is_srgb = true
	mat.uv1_triplanar = true
	mat.uv1_world_triplanar = true
	mat.texture_filter = BaseMaterial3D.TEXTURE_FILTER_LINEAR_WITH_MIPMAPS_ANISOTROPIC
	var mat_name = "material_" + id + ".tres"
	var mat_path = materials_path.path_join(col).path_join(mat_name)
	ResourceSaver.save(mat, mat_path)
	print("Material ", col, " ", id)

	# Create the scene and save it
	var cube = cube_scene.instantiate()
	cube.get_node("Mesh").material_override = load(mat_path)
	var new_cube_scene = PackedScene.new()
	new_cube_scene.pack(cube)
	var scene_name = col + "_" + id + ".tscn"
	var scene_path = scenes_path.path_join(col).path_join(scene_name)
	ResourceSaver.save(new_cube_scene, scene_path)
	print("Scene ", col, " ", id, " ", scene_path)

# Generates materials and scenes for all colors and textures
func generate_tools():
	# Iterate over all colors in textures
	for col in DirAccess.open(textures_path).get_directories():
		# Creates materials and scenes parent folders
		DirAccess.make_dir_recursive_absolute(materials_path.path_join(col))
		DirAccess.make_dir_recursive_absolute(scenes_path.path_join(col))
		# Get texture files of current color
		var tex_col_path = textures_path.path_join(col)
		var files = Array(DirAccess.open(tex_col_path).get_files())
		# Ignore .import files generated by Godot
		files = files.filter(func(s): return !s.ends_with(".import"))
		# Iterate over all texture files
		for tex in files:
			generate_tex(col, tex)

func _process(_delta):
	if generate:
		generate = false
		generate_tools()
